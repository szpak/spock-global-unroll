buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.3.4'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.codearte.nexus-staging'

scmVersion {
    tag {
        prefix = 'release'
        versionSeparator = '/'
    }
    createReleaseCommit = true
    releaseCommitMessage { version, position -> "Release version: ${version}\n\n[ci skip]" }
    ignoreUncommittedChanges = false
    hooks {
        pre 'fileUpdate', [file: 'README.md', pattern: {v, p -> /'info\.solidsoft\.spock:spock-global-unroll:.*'/},
                           replacement: {v, p -> "\'info.solidsoft.spock:spock-global-unroll:$v\'"}]
    }
}

project.group = "info.solidsoft.spock"
project.version = scmVersion.version

sourceCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.spockframework:spock-core:1.0-groovy-2.4'
    compileOnly 'org.codehaus.groovy:groovy-all:2.4.6'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.6'
    testCompile 'cglib:cglib-nodep:2.2.2' //for Spying in Spock
}

nexusStaging {
    packageGroup = "info.solidsoft"
    stagingProfileId = "34a3a5beeaa96"
}

modifyPom {
    project {
        name 'Spock Global Unroll Extension'
        description 'Spock extension automatically enabling parametrized tests unrolling in the whole project'
        url 'https://github.com/szpak/spock-global-unroll'
        inceptionYear '2016'

        scm {
            url 'https://github.com/szpak/spock-global-unroll'
            connection 'scm:https://github.com/szpak/spock-global-unroll'
            developerConnection 'scm:git://github.com/szpak/spock-global-unroll'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'szpak'
                name 'Marcin ZajÄ…czkowski'
                email 'mszpak ATT wp DOTT pl'
                roles {
                    role 'developer'
                    role 'despot'
                }
            }
        }
    }
}
